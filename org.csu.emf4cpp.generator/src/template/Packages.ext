/*
 * Packages.ext
 * Copyright (C) CÃ¡tedra SAES-UMU 2010 <andres.senac@um.es>
 * 
 * EMF4CPP is free software: you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * EMF4CPP is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
 
import org::eclipse::emf::ecore;

extension template::FQN;
extension template::Classes;
extension template::Member;
extension template::References;

Set[EPackage] getPackageIncludeFiles(EPackage this) :  
	this.EClassifiers.typeSelect(EClass).EAllStructuralFeatures.select(e|e.EContainingClass.EPackage != this).EContainingClass.EPackage.toSet()
;

Set[EClassifier] getExternalTypes(EPackage this) :  
    this.EClassifiers.typeSelect(EClass).EAllStructuralFeatures.EType.select(e|e.EPackage != this).toSet()
;

List[EStructuralFeature] getFeatures(EPackage this) :
	this.EClassifiers.typeSelect(EClass).EStructuralFeatures
;

List[EStructuralFeature] getAllFeatures(EPackage this) :
	getFeatures().addAll(
		this.ESubpackages.getAllFeatures()
	)
;

int getFeatureIDValue(EPackage this, EStructuralFeature ef) :
	if this.ESuperPackage == null then
		getAllFeatures().indexOf(ef)
	else
		getFeatureIDValue(ESuperPackage, ef)
;

String getFeatureMember(EPackage this, EStructuralFeature ef) :
    getMemberPrefix() + ef.EContainingClass.name + "__" + ef.name
;

String getFeatureGetMember(EPackage this, EStructuralFeature ef) :
    "get" + ef.EContainingClass.name + "__" + ef.name
;

cached List[EStructuralFeature] getStructuralFeatures(EPackage this) :
	this.EClassifiers.typeSelect(EClass).EAllStructuralFeatures.toSet().toList()
;

String getPackageFQN(EPackage this) :
	"::" + getFQN("::") + "::" + name.toFirstUpper() +"Package"
;

String getFeatureGetMemberCall(EPackage this, EStructuralFeature ef) :
    getPackageFQN() + "::_instance()->" + getFeatureGetMember(ef) + "()"
;

String getFeatureGetMemberCallOptional(EPackage this, EStructuralFeature ef,
    EReference ref) :
    getPackageFQN() + "::_instance() ? " + getPackageFQN() + "::_instance()->"
    + getFeatureGetMember(ef) + "() : ::ecore::EReference_ptr()"
;

String getFeatureGetMemberCallOrThis(EPackage this, EStructuralFeature ef) :
	"(dynamic_cast< " + getPackageFQN() + "* >(this)) ? dynamic_cast< " + getPackageFQN() + "* >(this)->" + getFeatureGetMember(ef) + "() " + ": " + getFeatureGetMemberCall(ef)
;

